use std::str::FromStr;
use expression::Predicate;
use expression::Term;
use expression::NotData;
use expression::AndData;
use expression::OrData;
use expression::ImpliesData;
use expression::IntegerComparisonData;
use expression::UnsignedBitVectorData;
use expression::VariableMappingData;
use expression::UnaryExpressionData;
use expression::BinaryExpressionData;
use expression::IntegerComparisonOperator;
use expression::IntegerUnaryOperator;
use expression::IntegerBinaryOperator;

grammar;

pub P1: Predicate = {
	<p1: P1> "&&" <p2: P2> => Predicate::And( AndData { p1: Box::new(p1), p2: Box::new(p2) } ),
	<p1: P1> "||" <p2: P2> => Predicate::Or( OrData { p1: Box::new(p1), p2: Box::new(p2) } ),
	<p1: P1> "->" <p2: P2> => Predicate::Implies( ImpliesData { p1: Box::new(p1), p2: Box::new(p2) } ),
	P2,
};

pub P2: Predicate = {
	"!!" <p: P2> => Predicate::Not( NotData { p: Box::new(p) } ),
	P3,
};

pub P3: Predicate = {
	<t1: A> <op: INT_COMP> <t2: A> => Predicate::IntegerComparison( IntegerComparisonData { op: op, t1: Box::new(t1), t2: Box::new(t2) } ),
	"true" => Predicate::BooleanLiteral(true),
	"false" => Predicate::BooleanLiteral(false),
	"(" <p: P1> ")" => p,
};

pub A: Term = {
	<a1: A> "[" <a2: A> "]" => Term::BinaryExpression( BinaryExpressionData { op: IntegerBinaryOperator::ArrayLookup, t1: Box::new(a1), t2: Box::new(a2) } ),
	T1,
};

pub T1: Term = {
	<t1: T1> <op: INT_BOP> <t2: T2> => Term::BinaryExpression( BinaryExpressionData { op: op, t1: Box::new(t1), t2: Box::new(t2) } ),
	T2,
};

pub T2: Term = {
	<op: INT_UOP> <t: T3> => Term::UnaryExpression( UnaryExpressionData { op: op, t: Box::new(t) } ),
	T3,
};

pub T3: Term = {
	<l:r"[0-9]+"> => Term::UnsignedBitVector( UnsignedBitVectorData { size: 64, value: u64::from_str(l).unwrap() } ),
	<v:r"[a-z, A-Z, '_'][a-z, A-Z, 0-9, '_']*"> => Term::VariableMapping( VariableMappingData { name: v.trim().to_string(), var_type: "".to_string() } ),
	"(" <t: T1> ")" => t,
};

pub INT_COMP: IntegerComparisonOperator = {
	"<" => IntegerComparisonOperator::LessThan,
	"<=" => IntegerComparisonOperator::LessThanOrEqual,
	">" => IntegerComparisonOperator::GreaterThan,
	">=" => IntegerComparisonOperator::GreaterThanOrEqual,
	"==" => IntegerComparisonOperator::Equal,
	"!=" => IntegerComparisonOperator::NotEqual,
};

pub INT_UOP: IntegerUnaryOperator = {
	"-" => IntegerUnaryOperator::Negation,
	"!" => IntegerUnaryOperator::BitwiseNot,
};

pub INT_BOP: IntegerBinaryOperator = {
	"+" => IntegerBinaryOperator::Addition,
	"-" => IntegerBinaryOperator::Subtraction,
	"*" => IntegerBinaryOperator::Multiplication,
	"/" => IntegerBinaryOperator::Division,
	"%" => IntegerBinaryOperator::Modulo,
	"&" => IntegerBinaryOperator::BitwiseAnd,
	"|" => IntegerBinaryOperator::BitwiseOr,
	"^" => IntegerBinaryOperator::BitwiseXor,
	"<<" => IntegerBinaryOperator::BitwiseLeftShift,
	">>" => IntegerBinaryOperator::BitwiseRightShift,
};